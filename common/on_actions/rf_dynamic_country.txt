on_monthly_pulse_country = {
    on_actions = {
        rf_war_counter
        rf_dynamic_country_ai_monthly
        rf_dynamic_country_ai_monthly_unrecognized
    }
}

rf_dynamic_country_ai_monthly = {
    trigger = {
        has_game_rule = ai_improvement_enabled
        is_country_type = recognized
        is_player = no
        is_subject = no
        is_at_war = no
        is_diplomatic_play_initiator = no
        is_diplomatic_play_target = no
        NOT = { has_variable = riseandfall_recent_war }
        num_states > 1
        # Performance: only run for countries that have at least one state showing
        # significant turmoil or very low market access (candidates for dynamic country creation)
        any_scope_state = {
            owner = root
            OR = {
                turmoil > 0.30
                market_access < 0.15
            }
        }
        # Ensure not involved in any diplomatic play (as ally or enemy)
        NOT = { any_country = { is_diplomatic_play_enemy_of = ROOT } }
    }
    effect = {
        rf_dynamic_country_ai_recognized = yes
    }
}

rf_dynamic_country_ai_monthly_unrecognized = {
    trigger = {
        has_game_rule = ai_improvement_enabled
        is_country_type = unrecognized
        is_subject = no
        is_player = no
        is_at_war = no
        is_diplomatic_play_initiator = no
        is_diplomatic_play_target = no
        NOT = { has_variable = riseandfall_recent_war }
        num_states > 1
        # Performance: only run for countries that have at least one state showing
        # significant turmoil or very low market access (candidates for dynamic country creation)
        any_scope_state = {
            owner = root
            OR = {
                turmoil > 0.30
                market_access < 0.15
            }
        }
        # Ensure not involved in any diplomatic play (as ally or enemy)
        NOT = { any_country = { is_diplomatic_play_enemy_of = ROOT } }
    }
    effect = {
        rf_dynamic_country_ai_unrecognized = yes
    }
}

rf_war_counter = {
    trigger = {
        has_game_rule = ai_improvement_enabled
        is_at_war = yes
    }
    effect = {
        rf_war_counter_se = yes
    }
}